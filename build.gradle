plugins {
    id 'java-library'
}

group = 'com.elikill58'

repositories {
    mavenCentral()
    jcenter()
    maven {
        name = 'spigot'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
    	url "https://jitpack.io"
    }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.16.1-R0.1-SNAPSHOT'
    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'
}

java.toolchain.languageVersion = JavaLanguageVersion.of(8)
    
archivesBaseName = 'GameAPI'

sourceSets.main {
    java.srcDirs = ['src']
    resources.srcDirs = ['resources']
}

compileJava.options.encoding = 'UTF-8'

import org.apache.tools.ant.filters.ReplaceTokens

processResources {
    from(sourceSets.main.resources.srcDirs) {
        // Only process this file, if world_base is processed its files are somehow corrupted
        include 'plugin.yml'
        filter ReplaceTokens, tokens: [version: version]
    }
}

def pluginDir = System.getProperty('plugin.dir')
if (pluginDir != null) {
    jar.destinationDirectory = file(pluginDir)
    jar.archiveFileName = 'GameAPI.jar'
}
